		ORG		0000H
		AJMP	MAIN
		ORG		0030H
MAIN:	MOV		P2,#00H
		MOV		P3,#0FFH
		MOV		R0,#00H
		MOV		R1,#0F7H

KEYSCN:	MOV		R0,#00H
		MOV		R1,#0F7H
KEYLP1:	;判断是否有键按下
		MOV		P3,#0F0H
		MOV		A,P3
		XRL		A,#0F0H
		JZ		KEYLP1
		ACALL	DELEYMS
		MOV		P3,#0F0H
		MOV		A,P3
		XRL		A,#0F0H
		JZ		KEYLP1

KEYLP2:	MOV		P3,R1
		MOV		A,P3
	   	JB		ACC.4,NEXT1;扫描第1列是否有按下
		MOV		A,#00H
		SJMP	NEXT5
NEXT1:	JB		ACC.5,NEXT2;第2列
		MOV		A,#01H
		SJMP	NEXT5
NEXT2:	JB		ACC.6,NEXT3;第3列
		MOV		A,#02H
		SJMP	NEXT5
NEXT3:	JB		ACC.7,NEXT4;第4列
		MOV		A,#03H
		SJMP	NEXT5
NEXT4:	; 扫描一下行
		JNB		ACC.0,EXIT
		MOV		A,R0
		ADD		A,#04H
		MOV		R0,A
		MOV		A,R1
		RR		A
		MOV		R1,A
		SJMP	KEYLP2
NEXT5:	ADD		A,R0
		MOV		R0,A
NEXT6:	MOV		A,P3
		XRL		A,R1
		JNZ		NEXT6 ;等待按键释放
EXIT:	MOV		A,R0
		MOV		DPTR,#TABLE2
		MOVC	A,@A+DPTR
		MOV		P2,A
		JB P1.2,$ ; 复位键
		MOV P2,#00H
		SJMP MAIN

DELEYMS: 
		MOV R7,#10	;delay 10ms   12Mhz  1us
DL0:	
		MOV R6,#0FAH ; 250*4 = 1000us = 1ms
DL1:
		NOP
		NOP
		DJNZ R6,DL1
		DJNZ R7,DL0
		RET
TABLE2: 
		DB	07H,7FH,6FH,71H	;7 8 9 F
		DB  66H,6DH,7DH,79H ;4 5 6 E
		DB  06H,5BH,4FH,5EH	;1 2 3 D
		DB  3FH,77H,7CH,39H	;0 A B C 
		END	